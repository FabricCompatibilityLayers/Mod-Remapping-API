plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'legacy-looming' version "1.10-SNAPSHOT" // Version must be the same as fabric-loom's
	id 'maven-publish'
	id 'com.gradleup.shadow' version '8.3.6'
	id 'xyz.wagyourtail.jvmdowngrader' version '1.3.0'
	id "com.modrinth.minotaur" version "2.+"
	id 'jacoco'
	id "org.sonarqube" version "5.0.0.4638"
}

version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()

	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}

	maven {
		name = "Jitpack"
		url = "https://jitpack.io/"
	}
}

loom.mods.register(project.name + "-testmod") {
	sourceSet project.sourceSets.test
}

dependencies {
	minecraft "net.minecraft:minecraft:${project.minecraft_version}"

	mappings legacy.yarn(project.minecraft_version, project.yarn_build)

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation(include(legacy.apiModule("legacy-fabric-logger-api-v1", project.fabric_version)))
	modImplementation(include("maven.modrinth:spasm:${project.spasm_version}"))

	shadow(runtimeOnly('net.fabricmc:mapping-io:0.7.1')) {
		transitive = false
	}
	shadow(runtimeOnly('net.fabricmc:tiny-remapper:0.11.2')) {
		transitive = false
	}
	shadow(runtimeOnly('net.fabricmc:access-widener:2.1.0')) {
		transitive = false
	}

	shadow(runtimeOnly('com.google.code.gson:gson:2.2.4')) {
		transitive = false
	}

	implementation(include("com.github.thecatcore.CursedMixinExtensions:fabric:1.0.0"))
	implementation(include("com.github.thecatcore:WFVAIO:1.2.1"))

	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
}

jvmdg {
	downgradeTo = JavaVersion.VERSION_1_8
	multiReleaseOriginal.set(true)
	multiReleaseVersions = [JavaVersion.VERSION_17]
}

base {
	archivesName = project.archives_base_name
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

shadowJar {
	configurations = [project.configurations.shadow]
	exclude("META-INF/**")

	relocate 'net.fabricmc.mappingio', 'io.github.fabriccompatibilitylayers.modremappingapi.impl.lib.mappingio'
	relocate 'net.fabricmc.tinyremapper', 'io.github.fabriccompatibilitylayers.modremappingapi.impl.lib.tinyremapper'
	relocate 'net.fabricmc.accesswidener', 'io.github.fabriccompatibilitylayers.modremappingapi.impl.lib.accesswidener'
	relocate 'com.google.gson', 'io.github.fabriccompatibilitylayers.modremappingapi.impl.lib.gson'
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

remapJar {
	// wait until the shadowJar is done
	dependsOn(shadowJar)
	mustRunAfter(shadowJar)
	// Set the input jar for the task. Here use the shadow Jar that include the .class of the transitive dependency
	input = shadowJar.archiveFile
	setArchiveClassifier("remapped")
}

downgradeJar {
	inputFile = remapJar.archiveFile
}

shadeDowngradedApi {
	setArchiveClassifier("")
}

test {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}

sonar {
	properties {
		property "sonar.host.url", System.getenv("SONAR_URL")
		property "sonar.token", System.getenv("SONAR_TOKEN")
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archives_base_name
			group = project.group
			version = project.version

			artifact shadeDowngradedApi
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "mod-remapping-api"
	changelog = """
Remove previous testing logging.

Fix excluding files from extracted libraries not working.
"""
	uploadFile = shadeDowngradedApi

	dependencies {
		embedded.version "spasm", project.spasm_version
	}
}

task testmodJar(type: Jar) {
	from sourceSets.test.output
	destinationDirectory = project.layout.buildDirectory.dir("libs")
	archiveClassifier.set("testmod")
}

build.dependsOn(testmodJar, shadeDowngradedApi)